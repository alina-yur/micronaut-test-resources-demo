pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
}

plugins {
    id 'me.champeau.includegit' version '0.1.5'
}

rootProject.name = "demo"

if (providers.gradleProperty("useDevelopmentVersions").map(Boolean::parseBoolean).getOrElse(false)) {
    gitRepositories {
        include('micronaut-gradle-plugin') {
            uri = 'git@github.com:micronaut-projects/micronaut-gradle-plugin.git'
            branch = 'master'
        }
        include('micronaut-test-resources') {
            uri = 'git@github.com:micronaut-projects/micronaut-test-resources.git'
            branch = '1.1.x'
        }
        include('native-build-tools') {
            uri = 'git@github.com:graalvm/native-build-tools.git'
            branch = 'master'
        }
    }
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }
    versionCatalogs {
        mn {
            from("io.micronaut:micronaut-bom:$micronautVersion")
        }
    }
}
